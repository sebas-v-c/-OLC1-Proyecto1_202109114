package gt.edu.usac.ingenieria.analyzer.statpy;
import java.util.stream.Collectors;
import java_cup.runtime.*;
import java.util.Arrays;
import java.util.ArrayList;
import gt.edu.usac.ingenieria.analyzer.errors.ErrorStpS;


// PARSER
parser code
{:
    ArrayList<ErrorStpS> errors = new ArrayList<>();
    public void syntax_error(Symbol s){
        errors.add(new ErrorStpS(s.left, s.right, s.value, SYM.terminalNames[s.sym]));
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        errors.add(new ErrorStpS("Syntax error, impossible to recover"));
    }
    public ArrayList<ErrorStpS> getErrors() {
        return errors;
        //return errors.stream().map(Object::toString).collect(Collectors.joining("\n-> "));
    }
    public boolean isSuccessExecution() {
        return !(errors.size() > 0);
    }
:}

/* TERMINAL */
terminal String KW_MAIN;
// data types
terminal String KW_VOID, KW_INT, KW_DOUBLE, KW_CHAR, KW_BOOL, KW_STRING;
// control structures
terminal String KW_IF, KW_ELSE, KW_SWITCH, KW_CASE, KW_DEFAULT;
terminal String KW_BREAK, KW_FOR, KW_DO, KW_WHILE, KW_CONTINUE, KW_RETURN;
// bools
terminal boolean KW_TRUE, KW_FALSE;
// literals
terminal String TK_STRING, TK_CHAR, TK_INT, TK_DOUBLE, TK_ID;
// operators
terminal String TK_INC, TK_DEC;
terminal String TK_PLUS, TK_MINUS, TK_MULT, TK_DIV;
terminal String TK_GEQ, TK_LEQ, TK_EQEQ, TK_NOTEQ;
terminal String TK_GREATER, TK_LESS, TK_AND, TK_OR, TK_NOT;
terminal String TK_UMINUS;
// groups
terminal String TK_LPAR, TK_RPAR, TK_LBRC, TK_RBRC, TK_LBRK, TK_RBRK;
// assign
terminal String TK_EQ;
// punctuation
terminal String TK_COMA, TK_SCOLON, TK_COLON;
// especial
terminal String TK_DOLL;

/* NON TERMINALS */
non terminal Init;
non terminal Main;
// instructions
non terminal Instructions;
non terminal Instruction;
non terminal IncreDecre;
// variable declaration
non terminal DeclareVar;
non terminal DataType;
// expresion control
non terminal Expresion;
non terminal Math;
non terminal Relation;
non terminal Logic;
non terminal ReasignVar;
// Conditional structures
non terminal IfStatement;
non terminal SwitchStatement, CaseDeft, Cases, Case, Default;
// loop structures
non terminal ForLoop, ForArgs, ForInitArg;
non terminal WhileLoop, DoWhileLoop;

// call a function
non terminal CallFunc;
non terminal ArgsList;

non terminal DeclareFunc;

non terminal CallJson, JsonArg;
// local environment
non terminal LocalEnv;
non terminal LocalEnvs;


/*PRECEDENCES*/
precedence left TK_OR;
precedence left TK_AND;
precedence right TK_NOT;
precedence left TK_EQEQ, TK_NOTEQ;
precedence left TK_LEQ, TK_GEQ, TK_LESS, TK_GREATER;
precedence left TK_PLUS, TK_MINUS;
precedence left TK_MULT, TK_DIV;
precedence right TK_UMINUS;

/* GRAMATIC */

start with Init;

// beginnig
Init            ::= Main;

Main            ::= KW_VOID KW_MAIN TK_LPAR TK_RPAR TK_LBRC Instructions TK_RBRC;

Instructions    ::=
                    Instructions Instruction
                |   Instruction
                ;

Instruction     ::=
                    DeclareVar TK_SCOLON
                |   DeclareFunc
                |   CallFunc TK_SCOLON
                |   IfStatement
                |   SwitchStatement
                |   ForLoop
                |   WhileLoop
                |   DoWhileLoop TK_SCOLON
                |   ReasignVar TK_SCOLON
                |   IncreDecre TK_SCOLON
                |   KW_BREAK TK_SCOLON
                |   KW_RETURN TK_SCOLON
                |   KW_RETURN Expresion TK_SCOLON
                |   KW_CONTINUE TK_SCOLON
                |   error
                ;

DeclareVar      ::=
                    DataType TK_ID TK_EQ Expresion
                |   DataType TK_ID
                ;

DataType        ::=
                    KW_INT
                |   KW_BOOL
                |   KW_DOUBLE
                |   KW_CHAR
                |   KW_STRING
                ;

ReasignVar      ::= TK_ID TK_EQ Expresion;

DeclareFunc     ::= KW_VOID TK_ID TK_LPAR TK_RPAR LocalEnv;

CallFunc        ::=
                    TK_ID TK_LPAR ArgsList TK_RPAR
                |   TK_ID TK_LPAR TK_RPAR
                ;

ArgsList        ::=
                    ArgsList TK_COMA Expresion
                |   Expresion
                ;

IfStatement     ::=
                    KW_IF TK_LPAR Expresion TK_RPAR LocalEnv KW_ELSE IfStatement
                |   KW_IF TK_LPAR Expresion TK_RPAR LocalEnv KW_ELSE LocalEnv
                |   KW_IF TK_LPAR Expresion TK_RPAR LocalEnv
                ;

LocalEnv        ::=
                    TK_LBRC Instructions TK_RBRC
                |   TK_LBRC TK_RBRC
                ;

SwitchStatement ::=
                    KW_SWITCH TK_LPAR Expresion TK_RPAR LocalEnvs;

LocalEnvs       ::=
                    TK_LBRC CaseDeft TK_RBRC
                |   TK_LBRC TK_RBRC
                ;

CaseDeft        ::=
                    Cases Default
                |   Cases
                |   Default
                ;

Cases           ::=
                    Cases Case
                |   Case
                ;

Case            ::=
                    KW_CASE Expresion TK_COLON Instructions
                |   KW_CASE Expresion TK_COLON
                ;

Default         ::=
                    KW_DEFAULT TK_COLON Instructions
                |   KW_DEFAULT TK_COLON
                ;

ForLoop         ::=
                    KW_FOR TK_LPAR ForArgs TK_RPAR LocalEnv;

ForArgs         ::= ForInitArg TK_SCOLON Expresion TK_SCOLON IncreDecre;

WhileLoop       ::= KW_WHILE TK_LPAR Expresion TK_RPAR LocalEnv;

DoWhileLoop     ::= KW_DO LocalEnv KW_WHILE TK_LPAR Expresion TK_RPAR;

ForInitArg      ::= KW_INT TK_ID TK_EQ TK_INT
                |   ReasignVar;


IncreDecre      ::=
                    TK_ID TK_INC
                |   TK_ID TK_DEC
                ;

CallJson        ::=
                    TK_DOLL TK_LBRC JsonArg TK_COMA JsonArg TK_COMA JsonArg TK_RBRC
                ;

JsonArg         ::=
                    TK_ID
                |   TK_STRING
                ;


Expresion       ::=
                    Math
                |   Relation
                |   Logic
                |   CallFunc
                |   CallJson
                |   IncreDecre
                |   TK_ID
                |   TK_STRING
                |   TK_CHAR
                |   TK_INT
                |   TK_DOUBLE
                |   KW_TRUE
                |   KW_FALSE
                |   TK_LPAR Expresion TK_RPAR
                ;

Math            ::=
                    Expresion TK_PLUS Expresion
                |   Expresion TK_MINUS Expresion
                |   Expresion TK_MULT Expresion
                |   Expresion TK_DIV Expresion
                |   TK_UMINUS Expresion                 %prec TK_UMINUS
                ;

Relation        ::=
                    Expresion TK_GEQ Expresion
                |   Expresion TK_LEQ Expresion
                |   Expresion TK_EQEQ Expresion
                |   Expresion TK_NOTEQ Expresion
                |   Expresion TK_GREATER Expresion
                |   Expresion TK_LESS Expresion
                ;

Logic           ::=
                    Expresion TK_AND Expresion
                |   Expresion TK_OR Expresion
                |   TK_NOT Expresion
                ;